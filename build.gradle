buildscript {
  ext {
    // SDK and plugins
    compileSdkVersion = 29
    buildToolsVersion = '29.0.2'
    minSdkVersion = 14
    targetSdkVersion = 29
    androidGradleVersion = '3.6.0-alpha12'
    gradleScanVersion = '2.4.2'
    versioningPluginVersion = '1.0.0'

    // Library versions
    kotlinVersion = '1.3.50'
    coroutinesVersion = '1.3.1'
    appcompatVersion = '1.1.0'
    coreKtxVersion = '1.2.0-alpha04'
    activityVersion = '1.1.0-alpha03'
    lifecycleVersion = '2.2.0-alpha05'
    materialVersion = '1.1.0-alpha10'
    recyclerviewVersion = '1.1.0-beta04'
    vectorDrawableVersion = '1.1.0'
    constraintlayoutVersion = '2.0.0-beta2'
    timberVersion = '4.7.1'
    daggerVersion = '2.24'
    versCompVersion = '1.3.3'
  }
  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$androidGradleVersion"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "com.gradle:build-scan-plugin:$gradleScanVersion"
    classpath "eu.appcom.gradle:android-versioning:$versioningPluginVersion"
  }
}

apply plugin: 'com.gradle.build-scan'

buildScan {
  termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter {
      content {
        includeModule 'com.g00fy2', 'versioncompare'
        includeModule 'org.jetbrains.trove4j', 'trove4j' // required by com.android.tools.lint:lint-gradle
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

task printR8Version() {
  try {
    //noinspection UnnecessaryQualifiedReference
    println "R8 version: " + com.android.tools.r8.Version.getVersionString()
  } catch (ignored) {
    println 'R8 version: unknown'
  }
}
